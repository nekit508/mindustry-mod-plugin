import com.github.nekit508.NMPlugin
import com.github.nekit508.tasks.core.GenerateModInfoTask

allprojects.each { project ->
    project.repositories {
        mavenCentral()
        mavenLocal()
        maven { url "https://raw.githubusercontent.com/Zelaux/MindustryRepo/master/repository" }
        maven { url "https://www.jitpack.io" }
    }

    project.apply plugin: "java"

    project.sourceSets.main.java.srcDirs = ["src"]
    project.sourceSets.main.resources.srcDirs = ["res"]
}

apply plugin: NMPlugin
apply plugin: "java-library"
apply plugin: "maven-publish"

nmp.core project, "core"

var isLib = true

core.configure {
    if (isLib) {
        java {
            withSourcesJar()
            withJavadocJar()
        }

        publishing {
            publications {
                library(MavenPublication) {
                    from components.java
                }
            }
        }
    }
    genericModInit(isLib)

    core.configureTasks {
        nmpGenerateModInfo { GenerateModInfoTask task ->
            task.set(
                    "author": "NaN",
                    "description": "Test mod.",
                    "repo": "https://github.com/nekit508/mindustry-mod-plugin",
                    "main": "core.Mod"
            )
        }
    }
}

core.settings {
    modName.set prop("modName") as String
    modGroup.set prop("modGroup") as String
    modVersion.set prop("modVersion") as String
    mindustryVersion.set prop("mindustryVersion") as String
    generateModInfo.set true
}